apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
//apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("private/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "org.orcatech.mobileresearchtool"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1       //TODO: update this version code for new releases
        versionName '1.0.0'  //TODO: update this version number for new releases
        setProperty("archivesBaseName", "ORCATECH-MRT-$versionName")
        buildConfigField("boolean", "BYPASS_MAIN_CONTENT", "true")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: ""]
            buildConfigField("boolean", "APP_IS_BETA", "false")
            buildConfigField("boolean", "APP_IS_DEV", "false")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['releaseDSN'] + "\"")
        }
        beta {
            versionNameSuffix("-beta")
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: "-beta"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "false")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['betaDSN'] + "\"")
        }
        development {
            versionNameSuffix("-development")
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: "-development"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "true")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['developmentDSN'] + "\"")
            debuggable true
        }
    }
    flavorDimensions "version"
    productFlavors {
        onnelaLabServer {
            dimension "version"
            manifestPlaceholders = [appName: "ORCATECH Mobile Research Tool"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", "false")
            buildConfigField("boolean", "READ_TEXT_AND_CALL_LOGS", 'true')
        }
        googlePlayStore {
            dimension "version"
            manifestPlaceholders = [appName: "ORCATECH Mobile Research Tool"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", 'true')
            buildConfigField("boolean", "READ_TEXT_AND_CALL_LOGS", 'false')
        }
        commStatsCustomUrl {
            dimension "version"
            manifestPlaceholders = [appName: "ORCATECH Mobile Research Tool"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", "true")
            buildConfigField("boolean", "READ_TEXT_AND_CALL_LOGS", 'true')
        }
    }
    useLibrary "org.apache.http.legacy"
    sourceSets {
        onnelaLabServer {
            manifest.srcFile 'src/commStatsCustomUrl/AndroidManifest.xml'
        }
        commStatsCustomUrl {
            manifest.srcFile 'src/textsAndCallsStats/AndroidManifest.xml'
        }
    }
}
// Don't include the default "debug" build variant as an option
android.variantFilter { variant ->
    if (variant.buildType.name == 'debug') {
        variant.setIgnore(true)
    }
}

repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.commonsware.cwac:anddown:0.3.0'
    implementation 'io.sentry:sentry-android:1.7.3'
    implementation 'com.madgag.spongycastle:core:1.54.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.google.android.material:material:1.3.0'
}